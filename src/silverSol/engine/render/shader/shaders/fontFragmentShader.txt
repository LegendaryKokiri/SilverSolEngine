#version 330

in vec2 passTextureCoordinates;

out vec4 outColor;

uniform sampler2D fontTextureAtlas;

uniform vec3 fontColor;
uniform float fontWidth;
uniform float fontEdgeWidth;

uniform vec3 borderColor;
uniform float borderWidth;
uniform float borderEdgeWidth;
uniform vec2 borderOffset;

void main(void) {
	float distance = 1.0 - texture(fontTextureAtlas, passTextureCoordinates).a;
	float alpha = 1.0 - smoothstep(fontWidth, fontWidth + fontEdgeWidth, distance);
	
	float borderAlpha = 0.0;
	if(borderWidth > 0.0) {
		float borderDistance = 1.0 - texture(fontTextureAtlas, passTextureCoordinates + borderOffset).a;
		borderAlpha = 1.0 - smoothstep(borderWidth, borderWidth + borderEdgeWidth, borderDistance);
	}
	
	float totalAlpha = alpha + (1.0 - alpha) * borderAlpha;
	vec3 totalColor = mix(borderColor, fontColor, alpha / totalAlpha);
	
	float textureAlpha = texture(fontTextureAtlas, passTextureCoordinates).a;
	outColor = vec4(totalColor, totalAlpha);
}